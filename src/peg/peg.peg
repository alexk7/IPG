# Hierarchical syntax
Grammar    <= Spacing Definition+ EndOfFile
Definition <= Identifier Spacing LEFTARROW Expression

Expression <= Sequence (SLASH Sequence)*
Sequence   <= Prefix*
Prefix     <= (AND / NOT)? Suffix
Suffix     <= Primary (QUESTION / STAR / PLUS)?
Primary    <= Identifier Spacing !LEFTARROW
            / OPEN Expression CLOSE
            / Literal / Class / DOT

# Lexical syntax
Identifier <= [a-zA-Z_][a-zA-Z0-9_]*

Literal   <= ['] (!['] Char)* ['] Spacing
           / ["] (!["] Char)* ["] Spacing
Class     <= '[' (!']' Range)* ']' Spacing
Range     <= Char ('-' Char)?
Char      <= '\\' ([nrt'"\[\]\\] / [1-9][0-9]*)
           / !'\\' .

LEFTARROW <= '<' ('-' / '=' / '<') Spacing
SLASH     <- '/' Spacing
AND       <- '&' Spacing
NOT       <- '!' Spacing
QUESTION  <- '?' Spacing
STAR      <- '*' Spacing
PLUS      <- '+' Spacing
OPEN      <- '(' Spacing
CLOSE     <- ')' Spacing
DOT       <- '.' Spacing

Spacing   <= (Space / Comment)*
Comment   <- '#' (!EndOfLine .)* EndOfLine
Space     <- ' ' / '\t' / EndOfLine
EndOfLine <- '\r\n' / '\n' / '\r'
EndOfFile <- !.
